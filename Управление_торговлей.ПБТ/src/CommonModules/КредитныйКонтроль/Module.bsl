Функция ПолучитьДатуПлатежаПоДатеДокумента(Знач ДатаДокумента, Знач Договор) Экспорт  //ПродажиСервер ПолучитьПоследнююДатуПоГрафику
	
	Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Договор) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	УсловияКонтроляВзаиморасчетовСрезПоследних.СрокКредита КАК Сдвиг,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаДокумента, ДЕНЬ, ЕСТЬNULL(УсловияКонтроляВзаиморасчетовСрезПоследних.СрокКредита, 0)), ДЕНЬ) КАК ДатаПлатежа,
		|	УсловияКонтроляВзаиморасчетовСрезПоследних.Календарь КАК Календарь
		|ИЗ
		|	РегистрСведений.УсловияКонтроляВзаиморасчетов.СрезПоследних(&ДатаДокумента, ДоговорКонтрагента = &Договор) КАК УсловияКонтроляВзаиморасчетовСрезПоследних");
			
	Иначе
		Возврат ДатаДокумента;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаДокумента", ДатаДокумента);
	Запрос.УстановитьПараметр("Договор", Договор);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДатаДокумента;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Календарь = Выборка.Календарь;
	
	Если ЗначениеЗаполнено(Календарь) Тогда
		ДатаПлатежа = КалендарныеГрафики.ДатаПоКалендарю(Календарь, ДатаДокумента, Выборка.Сдвиг);
	Иначе
		ДатаПлатежа = Выборка.ДатаПлатежа;
	КонецЕсли;
	
	Возврат ДатаПлатежа;
	
КонецФункции