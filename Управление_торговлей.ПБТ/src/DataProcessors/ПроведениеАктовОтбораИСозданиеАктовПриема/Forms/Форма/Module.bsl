
&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.ТЧ.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АктОтбВИ.Ссылка КАК Акт,
	|	АктПриемаОбразцов.Ссылка КАК АктПриема
	|ИЗ
	|	Документ.АктОтбора.ТЧ_ВидыИсследований КАК АктОтбВИ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктПриемаОбразцов КАК АктПриемаОбразцов
	|		ПО АктОтбВИ.Ссылка = АктПриемаОбразцов.ДокументОснование
	|			И (АктПриемаОбразцов.Лаборатория = &Лаборатория)
	|ГДЕ
	|	АктОтбВИ.Ссылка.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И АктОтбВИ.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И НЕ АктОтбВИ.Ссылка.Проведен
	|	И АктОтбВИ.Ссылка.Пользователь В(&Пользователь)
	|	И АктОтбВИ.Лаборатория = &Лаборатория
	|
	|СГРУППИРОВАТЬ ПО
	|	АктПриемаОбразцов.Ссылка,
	|	АктОтбВИ.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	АктОтбВИ.Ссылка.Дата";
	
	Запрос.УстановитьПараметр("КонПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("НачПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("Лаборатория", Лаборатория);
	Если СЗПользователи.Количество()>0 Тогда  //
		Запрос.УстановитьПараметр("Пользователь", СЗПользователи);
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И АктОтбВИ.Ссылка.Пользователь В(&Пользователь)", "");
	КонецЕсли; 
	Если ИПроведенные Тогда
	    Запрос.Текст = СтрЗаменить(Запрос.Текст, "И НЕ АктОтбВИ.Ссылка.Проведен", "");
	КонецЕсли; 
	Результат = Запрос.Выполнить();
	
	ВыборкаДетали = Результат.Выбрать();
	
	Пока ВыборкаДетали.Следующий() Цикл
		НовСтр = Объект.ТЧ.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетали);
		НовСтр.Проводить = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПровестиНаСервере()
	
	Для каждого СтрТЧ Из Объект.ТЧ Цикл
		
		Если Не СтрТЧ.Проводить Тогда
			Продолжить;
		КонецЕсли; 
		
		ДокОб = СтрТЧ.Акт.ПолучитьОбъект();
		
		//устанавливаем некоторые реквизиты документа
		Попытка 
			Если Не ЗначениеЗаполнено(ДокОб.ПервыйПровел) Тогда
				ДокОб.ПервыйПровел = Пользователи.ТекущийПользователь();
				ДокОб.Дата = ТекущаяДатаСеанса();
			КонецЕсли;
			ДокОб.Записать(РежимЗаписиДокумента.Запись);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Записан документ "+ДокОб);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При записи документа "+ДокОб);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;		
	    //проводим документ
		Попытка 
			ДокОб.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Проведен документ "+ДокОб);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При проведении документа "+ДокОб);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	ПровестиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИзменитьВсеНаСервере()
	
	Для каждого Стр Из Объект.ТЧ Цикл
		Стр.Проводить = НЕ Стр.Проводить;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВсе(Команда)
	ИзменитьВсеНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьАктыПриемаНаСервере()
	
	ТекПользователь = Пользователи.ТекущийПользователь();
	//СпрЛаб = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ТекПользователь, "ОсновнаяЛаборатория");
	//Если Не ЗначениеЗаполнено(СпрЛаб) Тогда
	//	СпрЛаб = Справочники.лаб_Лаборатории.НайтиПоКоду("3");//Лаборатория;
	//КонецЕсли; 
		
	Для каждого СтрТЧ Из Объект.ТЧ Цикл
		
		Если ЗначениеЗаполнено(СтрТЧ.АктПриема) Тогда
			Продолжить;
		КонецЕсли; 
		Если Не СтрТЧ.Проводить Тогда
			Продолжить;
		КонецЕсли; 
		
		АктПриема = Документы.АктПриемаОбразцов.СоздатьДокумент();
		
		АктПриема.Лаборатория = Лаборатория;
		АктПриема.Пользователь = ТекПользователь;
		АктПриема.Дата = ТекущаяДатаСеанса();
		
		АктПриема.Заполнить(СтрТЧ.Акт);
		
		Попытка
			АктПриема.Записать();
			СтрТЧ.АктПриема = АктПриема.Ссылка;
		Исключение
		    ОбщегоНазначенияКлиентСервер.СообщитьПользователю("не удалось записать акт приема для "+ СтрТЧ.Акт);
		КонецПопытки; 
		Попытка
		    АктПриема.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Исключение
		    ОбщегоНазначенияКлиентСервер.СообщитьПользователю("не удалось провести акт приема для "+ СтрТЧ.Акт);
		КонецПопытки;		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьАктыПриема(Команда)
	СоздатьАктыПриемаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Период.ДатаНачала = ТекущаяДата();
	Период.ДатаОкончания = ТекущаяДата();
КонецПроцедуры
