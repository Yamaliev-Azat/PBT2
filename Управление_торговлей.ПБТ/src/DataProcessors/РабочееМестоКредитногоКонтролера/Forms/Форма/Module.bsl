
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписок();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписок()

    ОбСписок = РеквизитФормыВЗначение("Список");
    ОбСписок.Строки.Очистить();	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(СоглашенияСКлиентамиЭтапыГрафикаОплаты.Сдвиг) КАК Сдвиг,
	               |	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Ссылка КАК Соглашение
	               |ПОМЕСТИТЬ ВТ_Сдвиги
	               |ИЗ
	               |	Справочник.СоглашенияСКлиентами.ЭтапыГрафикаОплаты КАК СоглашенияСКлиентамиЭтапыГрафикаОплаты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Контрагенты.Ссылка КАК Контрагент,
	               |	ДоговорыКонтрагентов.Ссылка КАК Договор,
	               |	СоглашенияСКлиентами.Ссылка КАК Соглашение,
	               |	ДоговорыКонтрагентов.ДопустимаяСуммаЗадолженности КАК ДопустимаяСуммаЗадолженности,
	               |	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	               |	СоглашенияСКлиентами.Календарь КАК Календарь,
	               |	ВТ_Сдвиги.Сдвиг КАК Отсрочка
	               |ИЗ
	               |	Справочник.Контрагенты КАК Контрагенты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |		ПО Контрагенты.Ссылка = ДоговорыКонтрагентов.Контрагент
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	               |		ПО Контрагенты.Ссылка = СоглашенияСКлиентами.Контрагент
	               |			И (ДоговорыКонтрагентов.Наименование = СоглашенияСКлиентами.Наименование)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сдвиги КАК ВТ_Сдвиги
	               |		ПО (СоглашенияСКлиентами.Ссылка = ВТ_Сдвиги.Соглашение)
	               |ИТОГИ ПО
	               |	Контрагент";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//ОбСписок = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
	
		НовСтр = ОбСписок.Строки.Добавить();
	    ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		
		ВыборкаДог = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДог.Следующий() Цикл
			СтрДог = НовСтр.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрДог, ВыборкаДог);
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбСписок, "Список");

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокДопустимаяСуммаЗадолженностиПриИзмененииНаСервере(ТекДоговор, Сумма)
	
	ДогОб = ТекДоговор.ПолучитьОбъект();
	ДогОб.ДопустимаяСуммаЗадолженности = Сумма;
	ДогОб.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДопустимаяСуммаЗадолженностиПриИзменении(Элемент)
	
	Сумма = Элементы.Список.ТекущиеДанные.ДопустимаяСуммаЗадолженности;
	ТекДоговор = Элементы.Список.ТекущиеДанные.Договор;
	СписокДопустимаяСуммаЗадолженностиПриИзмененииНаСервере(ТекДоговор, Сумма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокОтсрочкаПриИзмененииНаСервере(Соглашение, Отсрочка)
	
	CогОб = Соглашение.ПолучитьОбъект();
	Если CогОб.ЭтапыГрафикаОплаты.Количество()>0 Тогда
		ПерваяСтрока = CогОб.ЭтапыГрафикаОплаты[0];
	    ПерваяСтрока.Сдвиг = Отсрочка;
	КонецЕсли; 
	
	CогОб.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОтсрочкаПриИзменении(Элемент)
	
	Отсрочка = Элементы.Список.ТекущиеДанные.Отсрочка;
	Соглашение = Элементы.Список.ТекущиеДанные.Соглашение;
	
	СписокОтсрочкаПриИзмененииНаСервере(Соглашение, Отсрочка);
	
КонецПроцедуры
 