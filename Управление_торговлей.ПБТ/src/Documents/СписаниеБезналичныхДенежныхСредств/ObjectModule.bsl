
&После("ОбработкаПроведения")
Процедура Расш1_ОбработкаПроведения(Отказ, РежимПроведения)
	Если ЭтотОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплаты  
		ИЛИ 
		ЭтотОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета 
		ИЛИ
		ЭтотОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаботнику Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	вт_НачислениеЗарплатыОстатки.Организация КАК Организация,
		|	вт_НачислениеЗарплатыОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
		|	вт_НачислениеЗарплатыОстатки.ПериодНачисления КАК ПериодНачисления,
		|	вт_НачислениеЗарплатыОстатки.СуммаВзаиморасчетовОстаток КАК СуммаВзаиморасчетовОстаток
		|ИЗ
		|	РегистрНакопления.вт_НачислениеЗарплаты.Остатки(
		|			&МоментВремени,
		|			Организация = &Организация
		|				И ФизическоеЛицо = &ФизЛицо) КАК вт_НачислениеЗарплатыОстатки
		|ГДЕ
		|	вт_НачислениеЗарплатыОстатки.СуммаВзаиморасчетовОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодНачисления");
		
		Запрос.УстановитьПараметр("МоментВремени",Новый МоментВремени(ЭтотОбъект.Дата,ЭтотОбъект.Ссылка));
		Запрос.УстановитьПараметр("Организация",ЭтотОбъект.Организация);
		Запрос.УстановитьПараметр("ФизЛицо", ЭтотОбъект.ПодотчетноеЛицо);
		
		выборка = Запрос.Выполнить().Выбрать();
		
		СуммаКРаспределению = ЭтотОбъект.СуммаДокумента;
		
		ДвиженияЗП = Движения.вт_НачислениеЗарплаты; 
		
		пока 1=1 цикл
			 ЕстьСледующий = Выборка.Следующий();
			 
			 Если СуммаКРаспределению = 0 ТОгда
				 Прервать;
			 КонецЕсли;	 
			 
			 Если ЕстьСледующий ТОгда
				 СуммаСписания = ?(СуммаКРаспределению >  выборка.СуммаВзаиморасчетовОстаток, выборка.СуммаВзаиморасчетовОстаток, СуммаКРаспределению);
			 Иначе
				 СуммаСписания = СуммаКРаспределению;
			 КонецЕсли;
			 
			НоваяЗапись = ДвиженияЗП.Добавить();
			НоваяЗапись.ВидДвижения 		= ВидДвиженияНакопления.Расход;
			НоваяЗапись.Период 				= ЭтотОбъект.Дата;
			НоваяЗапись.ПериодНачисления 	= ?(ЕстьСледующий , выборка.ПериодНачисления, ЭтотОбъект.Дата);
			НоваяЗапись.Регистратор 		= ЭтотОбъект.Ссылка; 
			НоваяЗапись.Организация 		= ЭтотОбъект.Организация;
			НоваяЗапись.ФизическоеЛицо  	= ЭтотОбъект.ПодотчетноеЛицо;
			НоваяЗапись.СуммаВзаиморасчетов = СуммаСписания;
			
			СуммаКРаспределению = СуммаКРаспределению - СуммаСписания;
			
		КонецЦикла;
		
		
		
		ДвиженияЗП.Записать(Истина);
		
	КонецЕсли;	
КонецПроцедуры
