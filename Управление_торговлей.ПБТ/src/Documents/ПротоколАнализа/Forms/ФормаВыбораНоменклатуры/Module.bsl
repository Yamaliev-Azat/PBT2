
&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработкаВыбораСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораСтроки()

	Данные = Элементы.ТЗНом.ТекущиеДанные;
	Если Данные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не выбрана строка");
		Возврат;
	КонецЕсли;  
	ВыбранноеЗначение = Новый Структура("Номенклатура, Образец", Данные.Номенклатура, Данные.Образец);
	
	ОповеститьОВыборе(ВыбранноеЗначение);	

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Дата") Тогда
		ВидИсследования = Параметры.ВидИсследования;
		Дата = Параметры.Дата;
		Лаборатория = Параметры.Лаборатория;
	КонецЕсли;
	
	//заполнение остатков
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОтборОбразцовОстатки.Номенклатура КАК Номенклатура,
	               |	ОтборОбразцовОстатки.Образец КАК Образец,
	               |	ОтборОбразцовОстатки.КоличествоОстаток КАК Количество,
	               |	ОтборОбразцовОстатки.Лаборатория КАК Лаборатория,
	               |	ОтборОбразцовОстатки.ВидИсследования КАК ВидИсследования
	               |ИЗ
	               |	РегистрНакопления.ОтборОбразцов.Остатки(
	               |			&НаДату,
	               |			ВидДокумента = &ВидДокумента
	               |				И Лаборатория = &Лаборатория
	               |				И ВидИсследования = &ВидИсследования) КАК ОтборОбразцовОстатки
	               |ГДЕ
	               |	ОтборОбразцовОстатки.КоличествоОстаток > 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Номенклатура
	               |АВТОУПОРЯДОЧИВАНИЕ";
			     
	Запрос.УстановитьПараметр("НаДату", Дата);
	Запрос.УстановитьПараметр("ВидДокумента", Перечисления.лаб_ВидыДокументов.ПротоколАнализа);
	Запрос.УстановитьПараметр("ВидИсследования", ВидИсследования);
	Запрос.УстановитьПараметр("Лаборатория", Лаборатория);
	
	ТЗНом.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗНомПередНачаломИзменения(Элемент, Отказ)
	ОбработкаВыбораСтроки();
КонецПроцедуры
