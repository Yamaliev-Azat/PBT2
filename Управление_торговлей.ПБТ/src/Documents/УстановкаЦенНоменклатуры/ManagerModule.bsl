
&ИзменениеИКонтроль("ТекстЗапросаТаблицаЦеныНоменклатуры")
Функция Расш2_ТекстЗапросаТаблицаЦеныНоменклатуры(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ЦеныНоменклатуры";

	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура          КАК Номенклатура,
	|	ТаблицаТовары.Характеристика        КАК Характеристика,
	|	ТаблицаТовары.Упаковка              КАК Упаковка,
	|	ТаблицаТовары.ВидЦены               КАК ВидЦены,
	|	ТаблицаТовары.Цена                  КАК Цена,
	#Удаление
	|	ТаблицаТовары.ВидЦены.ВалютаЦены    КАК Валюта,
	#КонецУдаления
	#Вставка
	|	ТаблицаТовары.Валюта    КАК Валюта,
	#КонецВставки
	|	ТаблицаТовары.Ссылка.Дата           КАК ПЕРИОД
	|ИЗ
	|	Документ.УстановкаЦенНоменклатуры.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУстановокЦенНоменклатуры.Согласован)
	|	И ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
	|		ИЛИ (ТаблицаТовары.Номенклатура.ИспользованиеХарактеристик <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
	|		И ТаблицаТовары.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)))
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

	Возврат ТекстЗапроса;

КонецФункции
