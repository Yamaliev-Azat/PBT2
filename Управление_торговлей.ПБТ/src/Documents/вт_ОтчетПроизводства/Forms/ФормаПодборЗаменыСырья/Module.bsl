
&НаКлиенте
Процедура Отмена(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	Если ВсегоКоличествоАналога > тзнАналогиОстатки.Итог("Остаток") Тогда
		ПоказатьПредупреждение(,"Введено количество заменителей больше чем на остатках");
		Возврат;
	КонецЕсли;	
	ДанныеВыбора = ПеренестиВДокументНаСервере();
	ОповеститьОВыборе(ДанныеВыбора);
КонецПроцедуры

&НаСервере
Функция ПеренестиВДокументНаСервере()
	
	массивДанных = Новый Массив;
	ВсегоКоличествоИсх = 0;
	Для Каждого  ТекСтрокаТзн из тзнАналогиОстатки Цикл
		Если НЕ ТекСтрокаТзн.Выбран тогда
			Продолжить;
		КонецЕсли;	
		ЭлемДанных = Новый Структура("Номенклатура, Характеристика, Количество");
		ЗаполнитьЗначенияСвойств(ЭлемДанных, ТекСтрокаТзн);
		массивДанных.Добавить(ЭлемДанных);
		
		ВсегоКоличествоИсх = ВсегоКоличествоИсх + ТекСтрокаТзн.КоличествоИсходного;
		
	КонецЦикла;
	
	//Если осталось еще, то оставляем исходное сырье на указанную разницу
	Если КоличествоТребуется > ВсегоКоличествоИсх Тогда
		ЭлемДанных = Новый Структура("Номенклатура, Характеристика, Количество", Номенклатура, Характеристика, КоличествоТребуется - ВсегоКоличествоИсх );
		массивДанных.Добавить(ЭлемДанных);
	КонецЕсли;
	
	Возврат массивДанных;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	вт_АналогиНоменклатуры.Заменитель КАК Номенклатура,
	                      |	вт_АналогиНоменклатуры.ХарактеристикаЗаменитель КАК Характеристика,
	                      |	вт_АналогиНоменклатуры.Коэффициент КАК Коэффициент
	                      |ПОМЕСТИТЬ ВТ_АНАЛОГИ
	                      |ИЗ
	                      |	РегистрСведений.вт_АналогиНоменклатуры КАК вт_АналогиНоменклатуры
	                      |ГДЕ
	                      |	вт_АналогиНоменклатуры.Заменяемое = &Заменяемое
	                      |	И вт_АналогиНоменклатуры.ХарактеристикаЗаменяемое = &ХарактеристикаЗаменяемое
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЕСТЬNULL(ТоварыНаСкладахОстатки.ВНаличииОстаток, 0) - ЕСТЬNULL(ТоварыНаСкладахОстатки.КОтгрузкеОстаток, 0) - ЕСТЬNULL(СвободныеОстаткиОстатки.ВРезервеСоСкладаОстаток, 0) - ЕСТЬNULL(СвободныеОстаткиОстатки.ВРезервеПодЗаказОстаток, 0) КАК Остаток,
	                      |	ВТ_АНАЛОГИ.Коэффициент КАК Коэффициент,
	                      |	ВТ_АНАЛОГИ.Номенклатура КАК Номенклатура,
	                      |	ВТ_АНАЛОГИ.Характеристика КАК Характеристика
	                      |ПОМЕСТИТЬ ВТ_ИТОГ
	                      |ИЗ
	                      |	ВТ_АНАЛОГИ КАК ВТ_АНАЛОГИ
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	                      |				&МоментОстатков,
	                      |				(Номенклатура, Характеристика) В
	                      |						(ВЫБРАТЬ
	                      |							ВТ_АНАЛОГИ.Номенклатура КАК Номенклатура,
	                      |							ВТ_АНАЛОГИ.Характеристика КАК Характеристика
	                      |						ИЗ
	                      |							ВТ_АНАЛОГИ КАК ВТ_АНАЛОГИ)
	                      |					И Склад = &Склад) КАК ТоварыНаСкладахОстатки
	                      |		ПО ВТ_АНАЛОГИ.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	                      |			И ВТ_АНАЛОГИ.Характеристика = ТоварыНаСкладахОстатки.Характеристика
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(
	                      |				&МоментОстатков,
	                      |				(Номенклатура, Характеристика) В
	                      |						(ВЫБРАТЬ
	                      |							ВТ_АНАЛОГИ.Номенклатура КАК Номенклатура,
	                      |							ВТ_АНАЛОГИ.Характеристика КАК Характеристика
	                      |						ИЗ
	                      |							ВТ_АНАЛОГИ КАК ВТ_АНАЛОГИ)
	                      |					И Склад = &Склад) КАК СвободныеОстаткиОстатки
	                      |		ПО ВТ_АНАЛОГИ.Номенклатура = СвободныеОстаткиОстатки.Номенклатура
	                      |			И ВТ_АНАЛОГИ.Характеристика = СвободныеОстаткиОстатки.Характеристика
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_ИТОГ.Номенклатура КАК Номенклатура,
	                      |	ВТ_ИТОГ.Характеристика КАК Характеристика,
	                      |	ВТ_ИТОГ.Коэффициент КАК Коэффициент,
	                      |	ВТ_ИТОГ.Остаток КАК Остаток
	                      |ИЗ
	                      |	ВТ_ИТОГ КАК ВТ_ИТОГ
	                      |ГДЕ
	                      |	ВТ_ИТОГ.Остаток > 0");
	
	Если Параметры.Свойство("Номенклатура") Тогда
		Номенклатура = Параметры.Номенклатура;
	КонецЕсли;
	
	Если Параметры.Свойство("Характеристика") Тогда
		Характеристика = Параметры.Характеристика;
	КонецЕсли;
	
	Если Параметры.Свойство("Склад") Тогда
		Склад = Параметры.Склад;
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаОстатков") Тогда
		ДатаОстатков = Параметры.ДатаОстатков;
	КонецЕсли;
	
	Если Параметры.Свойство("Количество") Тогда
		КоличествоТребуется = Параметры.Количество;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Заменяемое",Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаЗаменяемое",Характеристика);
	Запрос.УстановитьПараметр("Склад",Склад);
	Запрос.УстановитьПараметр("МоментОстатков",ДатаОстатков);
	
	тзнАналогиОстатки.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Элементы.тзнАналогиОстаткиКоличествоИсходного.Заголовок = Номенклатура.Наименование;
КонецПроцедуры

&НаКлиенте
Процедура тзнАналогиОстаткиКоличествоПриИзменении(Элемент)
	ТекДанные = Элементы.тзнАналогиОстатки.ТекущиеДанные;
	Если ТекДанные.количество>ТекДанные.Остаток ТОгда
		ТекДанные.количество = ТекДанные.Остаток;
	КонецЕсли;	
	ТекДанные.КоличествоИсходного = ТекДанные.количество / ТекДанные.Коэффициент;
	ТекДанные.Выбран = ТекДанные.количество > 0;
	
	ОбновитьИтоги();
КонецПроцедуры

&НаКлиенте
Процедура тзнАналогиОстаткиКоличествоИсходногоПриИзменении(Элемент)
		ТекДанные = Элементы.тзнАналогиОстатки.ТекущиеДанные;
		ТекДанные.количество =  ТекДанные.КоличествоИсходного * ТекДанные.Коэффициент;
		
		Если ТекДанные.количество>ТекДанные.Остаток ТОгда
			 ТекДанные.количество = ТекДанные.Остаток;
			 ТекДанные.КоличествоИсходного = ТекДанные.количество / ТекДанные.Коэффициент;
		КонецЕсли;		
		ТекДанные.Выбран = ТекДанные.Количество > 0;
		
		ОбновитьИтоги();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтоги()
		ВсегоКоличествоАналога = тзнАналогиОстатки.Итог("Количество");
		ВсегоКоличествоИсх = тзнАналогиОстатки.Итог("КоличествоИсходного");
КонецПроцедуры
	